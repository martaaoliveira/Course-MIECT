
DROP PROCEDURE IF EXISTS ONLINEGAMES_PLATFORM.GetJogosOrdenadosPorVendas;
GO

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogosOrdenadosPorVendas
AS
BEGIN
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS [Platform], 
        J.PRECO AS [Price], 
        AVG(AV.AVALIACAO_VENDEDOR) AS [Seller review], 
        A.NIF_VENDEDOR AS [NIF Seller],
        ISNULL(C.Total_Sold, 0) AS [Total sold]
    FROM ONLINEGAMES_PLATFORM.JOGO J
    LEFT JOIN ONLINEGAMES_PLATFORM.ANUNCIOS A ON J.ANUNCIO_ID = A.ID
    LEFT JOIN ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR AV ON A.NIF_VENDEDOR = AV.NIF_VENDEDOR
    LEFT JOIN 
    (
        SELECT NOME_JOGO, ID_ANUNCIO, SUM(NR_ARTIGOS) as Total_Sold
        FROM ONLINEGAMES_PLATFORM.COMPRA
        GROUP BY NOME_JOGO, ID_ANUNCIO
    ) C 
    ON J.NOME = C.NOME_JOGO AND J.ANUNCIO_ID = C.ID_ANUNCIO
    GROUP BY 
        J.ANUNCIO_ID,
        J.STOCK,
        J.NOME,
        J.NOME_PLATAFORMA,
        J.PRECO,
        A.NIF_VENDEDOR,
        C.Total_Sold
    ORDER BY [Total sold] DESC;
END;
GO


drop procedure if exists ONLINEGAMES_PLATFORM.GetJogos;
go


CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogos
AS
BEGIN
    SET NOCOUNT ON;
    
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS Platform, 
        J.PRECO AS Price, 
        AV.AVALIACAO_VENDEDOR AS [Seller review],
        A.NIF_VENDEDOR AS [NIF Seller]
    FROM ONLINEGAMES_PLATFORM.JOGO J
    LEFT JOIN ONLINEGAMES_PLATFORM.ANUNCIOS A ON J.ANUNCIO_ID = A.ID
    LEFT JOIN ONLINEGAMES_PLATFORM.VENDEDOR V ON A.NIF_VENDEDOR = V.NIF
    LEFT JOIN (
        SELECT 
            NIF_VENDEDOR, 
            AVG(AVALIACAO_VENDEDOR) AS AVALIACAO_VENDEDOR
        FROM ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR
        GROUP BY NIF_VENDEDOR
    ) AV ON V.NIF = AV.NIF_VENDEDOR;
    
END

go

drop PROCEDURE if exists  ONLINEGAMES_PLATFORM.GetJogosPorPlataforma;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogosPorPlataforma
    @NOME_PLATAFORMA VARCHAR(50)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        J.ANUNCIO_ID AS [ID], 
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS [Platform], 
        J.PRECO AS [Price], 
        AV.AVALIACAO_VENDEDOR AS [Seller review], 
        A.NIF_VENDEDOR AS [NIF Seller]
    FROM 
        ONLINEGAMES_PLATFORM.JOGO J 
    JOIN 
        ONLINEGAMES_PLATFORM.ANUNCIOS A ON J.ANUNCIO_ID = A.ID
    LEFT JOIN 
        ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR AV ON A.NIF_VENDEDOR = AV.NIF_VENDEDOR
    WHERE 
        J.NOME_PLATAFORMA = @NOME_PLATAFORMA;
    
END
GO

drop  PROCEDURE if exists ONLINEGAMES_PLATFORM.GetJogosGeneroFavorito;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogosGeneroFavorito
    @NIF_Comprador VARCHAR(30)
AS
BEGIN
    SET NOCOUNT ON;
    
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS Platform, 
        J.PRECO AS Price,
        AVG(AV.AVALIACAO_VENDEDOR) AS [Seller review],
        A.NIF_VENDEDOR AS [NIF Seller],
        GP.Nome_Genero AS Genre
    FROM ONLINEGAMES_PLATFORM.JOGO J 
    JOIN ONLINEGAMES_PLATFORM.ANUNCIOS A ON J.ANUNCIO_ID = A.ID
    JOIN ONLINEGAMES_PLATFORM.TEM T ON J.ANUNCIO_ID = T.ID_ANUNCIO
    JOIN ONLINEGAMES_PLATFORM.GENERO_PREFERIDO GP ON T.Nome_Genero = GP.Nome_Genero
    LEFT JOIN ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR AV ON A.NIF_VENDEDOR = AV.NIF_VENDEDOR
    WHERE GP.NIF_Comprador = @NIF_Comprador
    GROUP BY 
        J.ANUNCIO_ID, 
        J.STOCK, 
        J.NOME, 
        J.NOME_PLATAFORMA, 
        J.PRECO, 
        A.NIF_VENDEDOR,
        GP.Nome_Genero;
    
END
GO
--------------------------------------------------
drop procedure if exists ONLINEGAMES_PLATFORM.ComprarJogo
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.ComprarJogo
    @NOME_JOGO VARCHAR(100),
    @ID_ANUNCIO INT,
    @NIF_COMPRADOR VARCHAR(30)
AS
BEGIN
    SET NOCOUNT ON;
   
    -- inserir a compra na tabela COMPRA
    INSERT INTO ONLINEGAMES_PLATFORM.COMPRA(NIF_COMPRADOR, NOME_JOGO, ID_ANUNCIO, NR_ARTIGOS)
    VALUES(@NIF_COMPRADOR, @NOME_JOGO, @ID_ANUNCIO, 1);

    RETURN 1; -- Retorna 1 se a operação for bem sucedida
END
GO

--inserir avaliação vendedor

DROP PROCEDURE IF EXISTS ONLINEGAMES_PLATFORM.InserirAvaliacao;
GO

CREATE PROCEDURE ONLINEGAMES_PLATFORM.InserirAvaliacao
    @NIF_COMPRADOR VARCHAR(30),
    @NIF_VENDEDOR VARCHAR(30),
    @AVALIACAO DECIMAL(3,1),
    @data_avaliacao DATETIME
AS
BEGIN
    SET NOCOUNT ON;
    INSERT INTO ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR
    (NIF_COMPRADOR, NIF_VENDEDOR, AVALIACAO_VENDEDOR, data_avaliacao)
    VALUES (@NIF_COMPRADOR, @NIF_VENDEDOR, @AVALIACAO, @data_avaliacao);
END
GO


----eliminar utilizador e os seus dados associados
drop Procedure if exists ONLINEGAMES_PLATFORM.eliminarutilizador;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.eliminarutilizador @NIF VARCHAR(30)
AS
BEGIN
    
    DELETE FROM ONLINEGAMES_PLATFORM.FAVORITOS WHERE NIF_COMPRADOR = @NIF;

	DELETE FROM ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR WHERE NIF_COMPRADOR = @NIF;
	
    DELETE FROM ONLINEGAMES_PLATFORM.GENERO_PREFERIDO WHERE NIF_Comprador = @NIF;
    
    DELETE FROM ONLINEGAMES_PLATFORM.AVALIACAO_JOGO WHERE NIF_COMPRADOR = @NIF;
    
    DELETE FROM ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR WHERE NIF_COMPRADOR = @NIF;
    
    DELETE FROM ONLINEGAMES_PLATFORM.COMPRA WHERE NIF_COMPRADOR = @NIF;
   
    DELETE FROM ONLINEGAMES_PLATFORM.Comprador WHERE NIF = @NIF;
    
    DELETE FROM ONLINEGAMES_PLATFORM.Vendedor WHERE NIF = @NIF;

    DELETE FROM ONLINEGAMES_PLATFORM.Utilizador WHERE NIF = @NIF;
END;
go


drop procedure if exists  ONLINEGAMES_PLATFORM.GetJogosOrdenadosPorVendedor;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogosOrdenadosPorVendedor
AS
BEGIN
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS Platform, 
        J.PRECO AS Price, 
        AVG(AV.AVALIACAO_VENDEDOR) AS [Seller review],
        A.NIF_VENDEDOR AS [NIF Seller]
    FROM 
        JOGO J
    JOIN 
        ANUNCIOS A ON J.ANUNCIO_ID = A.ID
    JOIN 
        VENDEDOR V ON A.NIF_VENDEDOR = V.NIF
    JOIN 
        AVALIACAO_VENDEDOR AV ON V.NIF = AV.NIF_VENDEDOR
    GROUP BY 
        J.ANUNCIO_ID, J.STOCK, J.NOME, J.NOME_PLATAFORMA, J.PRECO, A.NIF_VENDEDOR
    ORDER BY 
        AVG(AV.AVALIACAO_VENDEDOR) DESC;
END
GO



drop procedure if exists ONLINEGAMES_PLATFORM.sp_SearchJogo;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.sp_SearchJogo
    @searchText VARCHAR(100)
AS
BEGIN
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.STOCK, 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS [Platform], 
        J.PRECO AS [Price], 
        AV.AVALIACAO_VENDEDOR AS [Seller review],
        A.NIF_VENDEDOR AS [NIF Seller]
    FROM 
        ONLINEGAMES_PLATFORM.JOGO J
        LEFT JOIN ONLINEGAMES_PLATFORM.ANUNCIOS A ON J.ANUNCIO_ID = A.ID
        LEFT JOIN ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR AV ON A.NIF_VENDEDOR = AV.NIF_VENDEDOR
    WHERE 
        J.NOME LIKE '%' + @searchText + '%'
END
GO

drop PROCEDURE if exists  ONLINEGAMES_PLATFORM.GetAvaliacoesVendedor;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetAvaliacoesVendedor
    @NIF_Comprador VARCHAR(30)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        NIF_VENDEDOR AS [Seller NIF], 
        AVALIACAO_VENDEDOR AS [Seller Evaluation], 
        data_avaliacao AS [Evaluation Date]
    FROM 
        ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR
    WHERE 
        NIF_COMPRADOR = @NIF_Comprador;
    
END
GO

DROP PROCEDURE IF EXISTS ONLINEGAMES_PLATFORM.GetJogosComprados;
GO

CREATE PROCEDURE ONLINEGAMES_PLATFORM.GetJogosComprados
    @NIF_Comprador VARCHAR(30)
AS
BEGIN
    SELECT 
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS [Platform],
        AV.data_avaliacao AS [Purchase Time]
    FROM 
        ONLINEGAMES_PLATFORM.JOGO J 
    JOIN 
        ONLINEGAMES_PLATFORM.COMPRA C 
    ON 
        J.NOME = C.NOME_JOGO AND J.ANUNCIO_ID = C.ID_ANUNCIO
    JOIN
        ONLINEGAMES_PLATFORM.ANUNCIOS A
    ON
        A.ID = J.ANUNCIO_ID
    JOIN
        ONLINEGAMES_PLATFORM.AVALIACAO_VENDEDOR AV
    ON
        AV.NIF_COMPRADOR = C.NIF_COMPRADOR AND AV.NIF_VENDEDOR = A.NIF_VENDEDOR
    WHERE 
        C.NIF_COMPRADOR = @NIF_Comprador;
END;
GO


drop procedure if exists ONLINEGAMES_PLATFORM.sp_AddToFavorites;
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.sp_AddToFavorites
    @NIF_COMPRADOR VARCHAR(30),
    @NOME_JOGO VARCHAR(100),
    @ID_ANUNCIO INT
AS
BEGIN
    -- Verifica se o comprador e o jogo existem
    IF EXISTS (SELECT 1 FROM ONLINEGAMES_PLATFORM.Comprador WHERE NIF = @NIF_COMPRADOR)
       AND EXISTS (SELECT 1 FROM ONLINEGAMES_PLATFORM.Jogo WHERE NOME = @NOME_JOGO AND ANUNCIO_ID = @ID_ANUNCIO)
    BEGIN
        -- Se ambos existem, tenta adicionar o jogo à lista de favoritos do comprador
        BEGIN TRY
            INSERT INTO ONLINEGAMES_PLATFORM.Favoritos (NIF_COMPRADOR, NOME_JOGO, ID_ANUNCIO)
            VALUES (@NIF_COMPRADOR, @NOME_JOGO, @ID_ANUNCIO);
        END TRY
        BEGIN CATCH
            -- Se ocorrer algum erro, retorna a mensagem de erro
            SELECT ERROR_MESSAGE() AS ErrorMessage;
        END CATCH
    END
    ELSE
    BEGIN
        -- Se o comprador ou o jogo não existem, retorna uma mensagem de erro
        SELECT 'Comprador ou jogo não encontrado.' AS ErrorMessage;
    END
END;
GO


drop procedure if exists ONLINEGAMES_PLATFORM.sp_GetFavoriteGames
go

CREATE PROCEDURE ONLINEGAMES_PLATFORM.sp_GetFavoriteGames
    @NIF_COMPRADOR VARCHAR(30)
AS
BEGIN
    SELECT 
        J.ANUNCIO_ID AS [ID],
        J.NOME AS [Name], 
        J.NOME_PLATAFORMA AS [Platform], 
        J.PRECO AS [Price]
    FROM 
        ONLINEGAMES_PLATFORM.JOGO J
    JOIN 
        ONLINEGAMES_PLATFORM.FAVORITOS F ON J.NOME = F.NOME_JOGO AND J.ANUNCIO_ID = F.ID_ANUNCIO
    WHERE 
        F.NIF_COMPRADOR = @NIF_COMPRADOR
    ORDER BY 
        J.NOME;
END
GO

